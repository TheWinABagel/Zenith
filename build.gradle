plugins {
	id 'fabric-loom' version '1.6-SNAPSHOT'
	id 'maven-publish'
	id "com.modrinth.minotaur" version "2.+"
	id 'net.darkhax.curseforgegradle' version "1.1.16"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

def localFakerLib = findProject(':FakerLib') != null
def localAttributes = findProject(':ZenithAttributes') != null

repositories {
	maven { url = 'https://maven.parchmentmc.org' }						//Parchment mappings
	maven { url = "https://mvn.devos.one/snapshots/" } 					//Porting lib snapshots
	maven { url = "https://mvn.devos.one/releases/" }					//Porting lib releases (using both just in case)
	maven { url = 'https://maven.ladysnake.org/releases' }				//Cardinal Components
	maven { url = "https://api.modrinth.com/maven" }					//Projectile damage attribute,
	maven { url = "https://jitpack.io/" }								//Mixin Squared
	maven { url = "https://maven.jamieswhiteshirt.com/libs-release/" }	//Reach Entity Attribute
	maven { url = 'https://maven.blamejared.com' }
	maven { url = "https://maven.shedaniel.me" }						//REI, Arch
	maven { url = "https://maven.terraformersmc.com" }					//Modmenu
	maven { url = "https://cursemaven.com" }
	maven { url = "https://maven.bai.lol" }
	mavenLocal()														//Fakerlib, Zenith Attributes TODO Create maven/move to jitpack
	maven {
		name = "MrCrayfish (GitHub)"
		url = "https://maven.pkg.github.com/MrCrayfish/Maven"
		credentials {
			//username = project.findProperty("gpr.user") ?: System.getenv("GH_ID")
			username = project.findProperty("gpr.user") ?: "gogglebobby@gmail.com" //only used for gh, what a terrible name lol
			password = project.findProperty("gpr.key") ?: System.getenv("GH_TOKEN")
		}
		content {
			includeGroup "com.mrcrayfish"
		}
	}

}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-1.20.1:2023.09.03@zip")
	}
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

//	if (localFakerLib) {
//		modImplementation("dev.bagel.fakerlib:FakerLib:${project.fakerlib_version}")
//	}
//	else {
		modImplementation("maven.modrinth:fakerlib:${project.fakerlib_version}")
//	}
//	if (localAttributes) {
//		modImplementation("dev.bagel:ZenithAttributes:${project.zenith_attributes_version}")
//	}
//	else {
		modImplementation("maven.modrinth:zenith-attributes:${project.zenith_attributes_version}")
//	}

	// Mixin^2 for mod compat
	implementation(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-fabric:${project.mixin_squared_version}"))

	implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:${project.mixin_extras_version}"))

	modImplementation(include("io.github.fabricators_of_create.Porting-Lib:base:${project.port_lib_version}"))
	modImplementation(include("io.github.fabricators_of_create.Porting-Lib:loot:${project.port_lib_version}"))
	modImplementation(include( "io.github.fabricators_of_create.Porting-Lib:utility:${project.port_lib_version}"))
	modImplementation(include("io.github.fabricators_of_create.Porting-Lib:tags:${project.port_lib_version}"))
	modImplementation(include("io.github.fabricators_of_create.Porting-Lib:tool_actions:${project.port_lib_version}"))
	modImplementation(include("io.github.fabricators_of_create.Porting-Lib:models:${project.port_lib_version}"))
	modImplementation(include("io.github.fabricators_of_create.Porting-Lib:model_loader:${project.port_lib_version}"))
	modImplementation(include("io.github.fabricators_of_create.Porting-Lib:model_materials:${project.port_lib_version}"))



	modCompileOnly modRuntimeOnly("vazkii.patchouli:Patchouli:${project.patchouli_version}"), {
		exclude(group: "net.fabricmc.fabric-api")
	}

	// Fabric ASM
	modImplementation(include("com.github.Chocohead:Fabric-ASM:${project.fabric_asm_version}"))

	// CCA + PAL
	modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cardinal_components_version}")
	modImplementation("io.github.ladysnake:PlayerAbilityLib:${project.pal_version}")

	// ModMenu
	modCompileOnly modRuntimeOnly("com.terraformersmc:modmenu:${project.modmenu_version}"), {
		exclude(group: "net.fabricmc.fabric-api")
	}

	// Trinkets
	modCompileOnly("dev.emi:trinkets:${project.trinkets_version}")
//	modRuntimeOnly("dev.emi:trinkets:${project.trinkets_version}")

	// WTHIT
	modCompileOnly "mcp.mobius.waila:wthit-api:fabric-${project.wthit_version}"

	//Jade
	modCompileOnly "curse.maven:jade-324717:${jade_id}"

	// REI
	modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${project.rei_version}"
	modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${project.rei_version}"

	// EMI
	modCompileOnly "dev.emi:emi-fabric:${project.emi_version}"

	switch (block_viewer.toLowerCase(Locale.ROOT)) {
		case "wthit": 	modRuntimeOnly "mcp.mobius.waila:wthit:fabric-${project.wthit_version}"
						modRuntimeOnly "lol.bai:badpackets:fabric-${project.badpackets_version}"; break
		case "jade": modRuntimeOnly "curse.maven:jade-324717:${jade_id}"; break
		case "disabled": break
		default: println("Unknown block viewer specified: $recipe_viewer. Must be Jade, WTHIT, or disabled.")
	}

	switch (recipe_viewer.toLowerCase(Locale.ROOT)) {
		case "rei": modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
					modRuntimeOnly "dev.architectury:architectury-fabric:${project.arch_version}"; break
		case "emi": modRuntimeOnly("dev.emi:emi-fabric:${project.emi_version}"); break
		case "disabled": break
		default: println("Unknown recipe viewer specified: $recipe_viewer. Must be EMI, REI or disabled.")
	}


	//Fixes +compat for other mods
	//Spell power + Spell engine
	modCompileOnly(("maven.modrinth:spell-power:${project.spell_power_version}-fabric"))
	modCompileOnly(("maven.modrinth:spell-engine:${project.spell_engine_version}-fabric"))


	//puzzlelib
	modCompileOnly "curse.maven:puzzles-lib-495476:4847147"

	//dragon loot
	modCompileOnly "curse.maven:dragonloot-424480:4656260"
	modRuntimeOnly("me.shedaniel.cloth:cloth-config-fabric:11.1.106") { //cloth config, required by dragon loot
		exclude(group: "net.fabricmc.fabric-api")
	}

	//modCompileOnly "curse.maven:mariums-soulslike-weaponry-632066:4891244" //soulslike weaponry

	//goblin traders
	//modCompileOnly "com.mrcrayfish:framework-fabric:1.20.1-0.6.13"
	//modRuntimeOnly "com.mrcrayfish:catalogue-fabric:1.20.1-1.7.1"
//	modCompileOnly "com.mrcrayfish:goblintraders-fabric:1.20.1-1.9.3"

	//modern industrialization
	modCompileOnly "curse.maven:modern-industrialization-405388:4775647", {
		exclude(group: "me.shedaniel")
	}

	//Iceberg
	modCompileOnly "curse.maven:iceberg-fabric-539382:4953470"
	modCompileOnly "curse.maven:equipment-compare-fabric-541329:4940609"

	//spectrum
	modCompileOnly "com.github.DaFuqs:Spectrum:${project.spectrum_version}"

	//YUNGS api
	modCompileOnly "curse.maven:yungs-api-fabric-421649:5147000"

}
loom {
	accessWidenerPath = file("src/main/resources/zenith.aw")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version, "loader_version": project.loader_version, "fakerlib_version": project.fakerlib_version, "zenith_attributes_version": project.zenith_attributes_version, "fapi_version": project.fabric_version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {

}


jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}

}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {

	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "zenith"
	versionType = "release"
	versionName = "Zenith " + project.version
	uploadFile = remapJar
	gameVersions = ["1.20.1"]
	loaders = ["fabric"]
	changelog = "[Changelog](https://github.com/TheWinABagel/Zenith/blob/1.20/changelog.md)"
	//debugMode = true
	dependencies {
		required.project "fabric-api"
		required.project("fakerlib")
		required.project("zenith-attributes")
		embedded.project("porting_lib")
	}
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
	apiToken = System.getenv("cf_token")
	//debugMode = true
	def mainFile = upload(620928, remapJar)
	mainFile.changelog = "https://github.com/TheWinABagel/Zenith/blob/1.20/changelog.md"
	mainFile.releaseType = "release"
	//mainFile.addJavaVersion("17")
	mainFile.addGameVersion("1.20.1")
	mainFile.addModLoader("fabric")
	mainFile.displayName = project.archives_base_name + "-" + project.version
	mainFile.addRequirement("fakerlib") //Fakerlib
	mainFile.addRequirement("zenith-attributes") //ZenithAttributes
	mainFile.addRequirement("fabric-api") //FAPI
	mainFile.addRequirement("patchouli-fabric") //Patchouli
	mainFile.addOptional("roughly-enough-items") //REI
	mainFile.addOptional("enchantment-descriptions") //Enchantment Descriptions
}
